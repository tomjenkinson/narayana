########################################################################
#
# Byteman script used to make BytemanControlledRecord fail
# topLevelCommit until AtomicActionRecoveryModule acknowledges that
# it needs to block the suspension of PeriodicRecovery.
# The end goal of this script is to simulate a transaction that fails
# while PeriodicRecovery is trying to recover it. This scenario makes
# sure that PeriodicRecovery waits to suspend until all transactions
# have recovered
#
########################################################################

########################################################################
# Rule to set waitForRecoveryFlag when invoking
# RecoveryEnvironmentBean.isWaitForRecovery() in
# PeriodicRecovery.suspendScan(boolean)
#########################################################################

RULE setWaitForRecoveryFlag
CLASS com.arjuna.ats.internal.arjuna.recovery.PeriodicRecovery
METHOD suspendScan
AFTER INVOKE isWaitForRecovery
BIND returnValue:boolean = $!
IF returnValue
DO debug("[PeriodicRecovery] Setting waitForRecoveryFlag to true"),
   flag("waitForRecoveryFlag")
ENDRULE

########################################################################
# Rule to clear greenFlag when invoking 
# BytemanControlledRecord.resetGreenFlag()
#########################################################################

RULE resetGreenFlag
CLASS com.hp.mwtests.ts.arjuna.resources.BytemanControlledRecord
METHOD resetGreenFlag
AT ENTRY
IF TRUE
DO debug("[BytemanControlledRecord] clearing greenFlag"),
   clear("greenFlag")
ENDRULE

########################################################################
# Rule to set greenFlag when
# AtomicActionRecoveryModule.hasWork returns true
#########################################################################

RULE setGreenFlag
CLASS com.arjuna.ats.internal.arjuna.recovery.AtomicActionRecoveryModule
METHOD hasWork
AT EXIT
BIND returnValue:boolean = $!
IF returnValue && flagged("waitForRecoveryFlag")
DO debug("[AtomicActionRecoveryModule] Setting greenFlag to true"),
   flag("greenFlag")
ENDRULE

########################################################################
# Rule to artificially set greenFlag when
# AtomicActionRecoveryModule.hasWork returns true
#########################################################################

RULE artificiallySetGreenFlag
CLASS com.hp.mwtests.ts.arjuna.resources.BytemanControlledRecord
METHOD setGreenFlag
AT ENTRY
IF TRUE
DO debug("[BytemanControlledRecord] artificially setting greenFlag"),
   flag("greenFlag")
ENDRULE

########################################################################
# Rule to fail
# BytemanControlledRecord.topLevelCommit()
# until greenFlag gets set
#########################################################################

RULE FailTopLevelCommit
CLASS com.hp.mwtests.ts.arjuna.resources.BytemanControlledRecord
METHOD topLevelCommit
AT EXIT
IF NOT flagged("greenFlag")
DO debug("[BytemanControlledRecord] returning TwoPhaseOutcome.FINISH_ERROR"),
   # TwoPhaseOutcome.FINISH_ERROR == 8
   return 8
ENDRULE